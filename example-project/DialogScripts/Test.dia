--HelloWorld--
Devs: Hello world! Thanks for trying out our dialog system!
[goto Examples]

--Examples--
Devs: What feature would you like to see an example of?
? prompt
    Devs: Adding a \[prompt\] tag adds a prompt[prompt] after[prompt] every[prompt] word.
? page
    Devs: ^^Even though this is a single dialog line, when it hits a \[page\] tag...\n
        [page]...it'll scroll the current line to a new page to read the rest!^^
? goto
    Devs: ^^The name of this section is "Examples". \[goto Examples\] will return you 
        to the beginning of the section. Let's go!^^
    [goto Examples]
? speed
    Devs: This text will be read at regular speed.
    Devs: [speed=0.5]This will be read at half speed because of the tag \[speed=0.5\].
    Devs: [speed=2]And this will be read at double speed due to a tag of \[speed=2\]!
? pause
    Devs: This text inserts a pause here[pause=2] then continues on!
? auto
    Devs: ^^If you add an "auto" tag to the end of a line, it will automatically proceed after 
        a pause.^^
    Devs: This line ends with \[auto=2\].[auto=2]
    Devs: ^^You can also set an "auto" tag on its own line. This will make all lines auto-proceed 
        until you turn it off.^^
    [auto]
    Devs: ^^We just turned it on and didn't set a pause time (\[auto\]) so it'll auto proceed 
        based on the length of the line.^^
    Devs: This will also auto-proceed. 
    [/auto]
    Devs: \[/auto\] closes the tag, so you'll need to proceed manually from here!
? members
    Devs: ^^You can define methods and properties to be used in your scripts! To do so, 
        make an additional partial class to go with your Dialog class that is named 
        *.DialogBridge.cs. We made one and added a "Flash()" method! Let's try it.^^
    Devs: Flash![Flash()]
    Devs: Go to Dialog.DialogBridge.cs to take a look!
? await
    Devs: ^^In Dialog.DialogBridge we created a custom method, Shake(), that shakes the dialog box. 
        Internally, it's handled asynchronously (Take a look!). You can wait for it to finish 
        before continuing with the script by writing \[await Shake()\][await Shake()], or if you 
        don't want to wait, simply call the method name.^^
    Devs: \[Shake()\][Shake()]. Notice that it's still writing during the effect. Cool, right?
? end
    Devs: \[end\] ends the script. Goodbye!
    [end]