--HelloWorld--
// Helpers for coloring tags
[ltag = "[color=Gold][lb]"]
[rtag = "[rb][/color]"]
[#OptionColumns=3]
Devs: The dialog system supports helper tags you can put between square brackets.
[goto Examples]

--Examples--
Devs: What tag would you like to see an example of?
? prompt
    Devs: ^^Adding a [ltag]prompt[rtag] tag will cause the line to prompt even if it's not 
        the end of a line.^^
    Devs: In this case we added a prompt[prompt] after[prompt] every[prompt] word.
? scroll
    Devs: [ltag]scroll[rtag]
? page
    Devs: ^^[ltag]page[rtag] is a combination of [ltag]prompt[rtag], a linebreak, and 
        [ltag]scroll[rtag] all in one!^^
    Devs: ^^
        Even though this is a single dialog line, when it hits a [ltag]page[rtag] tag...
        [page]...it'll scroll the current line to a new page to read the rest!
        ^^
? goto
    Devs: ^^The name of this section is "Examples". [ltag]goto Examples[rtag] will return you 
        to the beginning of the section. Let's go!^^
    [goto Examples]
? speed
    Devs: The "speed" tag will change the speed to a multiplier of the default.
    Devs: [speed=0.5]This will be read at half speed because of a [ltag]speed=0.5[rtag] tag.
    Devs: [speed=2]And this will be read at double speed due to a tag of [ltag]speed=2[rtag]!
? pause
    Devs: You can can pause the script for a set amount of time using the "pause" tag.
    Devs: We've added [ltag]pause=2[rtag] here[pause=2] so it stops for two seconds before continuing!
? auto
    Devs: ^^If you add an [ltag]auto[rtag] tag to the end of a line, it will automatically 
        proceed after a pause based on the length of the text being written.^^
    Devs: You can also set a specific amount of time to pause in seconds.
    Devs: This line ends with [ltag]auto=2[rtag].[auto=2]
    Devs: ^^You can also set an auto tag on its own line. This will make all lines auto-proceed 
        until you turn it off.^^
    [auto]
    Devs: Adding an auto tag on its own line will set it globally. We just did so this will auto-proceed.
    Devs: It'll keep auto-proceeding until you turn it off by using a closing tag [ltag]/auto[rtag].
    Devs: We'll do that after this line.
    [/auto]
    Devs: It just turned off, so you'll need to proceed manually from here!
? end
    Devs: [ltag]end[rtag] ends the script. Goodbye!
    [end]